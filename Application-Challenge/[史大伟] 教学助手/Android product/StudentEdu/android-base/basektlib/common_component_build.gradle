Properties props = new Properties()
props.load(project.rootProject.file('local.properties').newDataInputStream())
if (props.isBuildModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }


    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        testInstrumentationRunner rootProject.ext.dependencies.androidJUnitRunner
        buildConfigField "boolean", "IS_BUILD_MODULE", "${props.isBuildModule.toBoolean()}"
        multiDexEnabled true
        //Arouter路由配置 java配置
//        javaCompileOptions {
//            annotationProcessorOptions {
//                arguments = [AROUTER_MODULE_NAME: project.getName()]
//            }
//        }
        // consumerProguardFiles，然后 aar 库项目就会把 proguard 文件打包到 aar 输出中，app 项目用到这中 aar，会在打包时自动应用 aar 中自带的 proguard 文件。完美解决了此类问题。
        consumerProguardFiles "consumer-rules.pro"
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

    }

    sourceSets {
        main {
            if (props.isBuildModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    dexOptions {
        //使用增量模式构建
        incremental true
        //最大堆内存
        javaMaxHeapSize "8g"  //注意内存是自己电脑内存大小配置
        //是否支持大工程模式
        jumboMode = true
        //预编译
        preDexLibraries = true
        //线程数
        threadCount = 8
    }
    androidExtensions {
        experimental = true
    }
}

//Arouter路由配置 kotlin配置
//kapt {
//    arguments {
//        arg("AROUTER_MODULE_NAME", project.getName())
//    }
//}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    if(props.isBuildModule.toBoolean()){
        testImplementation rootProject.ext.dependencies.test.junit
        androidTestImplementation rootProject.ext.dependencies.test.runner
        androidTestImplementation rootProject.ext.dependencies.test.espresso_core
    }
    kapt rootProject.ext.dependencies.arouter.arouter_compiler
    kapt rootProject.ext.dependencies.butterknife.butterknife_compiler
    kapt rootProject.ext.dependencies.tools.moshi_kotlin_codegen
    kapt rootProject.ext.dependencies.tools.autopage_processor
}
